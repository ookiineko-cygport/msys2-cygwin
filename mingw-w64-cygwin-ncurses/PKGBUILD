pkgname=cygwin-ncurses
pkgver=6.5_20240427
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library (Cygwin)"
arch=('any')
url="https://invisible-island.net/ncurses/ncurses.html"
license=('MIT')
groups=('cygwin-toolchain')
depends=('cygwin' 'cygwin-gcc')
makedepends=('autoconf-archive' 'ncurses' 'cygwin-libtool' 'cygwin-configure')
optdepends=('bash: for ncursesw6-config')
options=('!strip' 'staticlibs' '!libtool' '!buildflags')
source=("https://invisible-mirror.net/archives/ncurses/current/ncurses-${pkgver/_/-}.tgz"
        ncurses-6.3-libs.patch
        ncurses-6.3-pkgconfig.patch
        cygwin-ncurses-abi.patch
        cygwin-terminfo.patch)
sha256sums=('abf30fcb0f97706cb3f62703595c561515f6b7c257dce2e3c8dee7d83afdfa76'
            'dc4261b6642058a9df1c0945e2409b24f84673ddc3a665d8a15ed3580e51ee25'
            'b8544a607dfbeffaba2b087f03b57ed1fa81286afca25df65f61b04b5f3b3738'
            'ccc61018e8683b7184625e8a5143e63c3a4c7aaa65df30feb69a81bfc913ca9f'
            '8a020ec167ac97eebd9c1971e90ddd6a74a7811072875bfdc968c28d6617f34a')

_targets="x86_64-pc-cygwin"

prepare() {
  cd ncurses-${pkgver/_/-}
  patch -Np1 -i ../cygwin-ncurses-abi.patch
  patch -Np1 -i ../cygwin-terminfo.patch
  # do not link against test libraries
  patch -Np1 -i ../ncurses-6.3-libs.patch
  # do not leak build-time LDFLAGS into the pkgconfig files:
  # https://bugs.archlinux.org/task/68523
  patch -Np1 -i ../ncurses-6.3-pkgconfig.patch
  # NOTE: can't run autoreconf because the autotools setup is custom and ancient
}

build() {
  local configure_opts=(
    --without-ada
    --with-cxx
    --with-cxx-shared
    --with-shared
    --with-normal
    --with-ticlib
    --with-build-cflags=-D_XOPEN_SOURCE_EXTENDED
    --with-abi-altered=10
    --with-wrap-prefix=ncwrap_
    --with-xterm-kbs=BS
    --with-terminfo-dirs=/usr/share/terminfo
    --with-default-terminfo-dir=/usr/share/terminfo
    --disable-relink
    --enable-lp64
    --enable-reentrant
    --enable-pc-files
    --disable-rpath
    --enable-overwrite
    --enable-colorfgbg
    --enable-ext-colors
    --enable-ext-mouse
    --enable-symlinks
    --enable-warnings
    --enable-assertions
    --enable-sp-funcs
    --enable-widec
    --without-manpages
    --without-tests
    --without-debug)

  cd ncurses-${pkgver/_/-}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure \
        --with-libtool=/usr/${_target}/bin/libtool \
        "${configure_opts[@]}"
    # taken from ncurses.cygport
    sed -i -e 's%^\(LIBRARIES[ \t]*=\).*$%\1 ../lib/libncursesw.la ../lib/libticw.la%' ncurses/Makefile
    make
    popd
  done
}

package() {
  cd ncurses-${pkgver/_/-}

  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)

    find "$pkgdir"/usr/${_target} -name '*.exe' -delete
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g

    # fixup terminfo
    if [[ ! -d "$pkgdir"/usr/share/cygwin-terminfo ]]; then
        pushd "$pkgdir"/usr/share/terminfo
          # cygwin and mingw has case insensitive filesystem, host tic installs it wrongly
          for _subdir in $(find -maxdepth 1 -type d -name '[0-9a-zA-Z]' -exec basename {} \;); do
            mv -T "$_subdir" "$(printf '%02x\n' "'${_subdir}'")"
          done
        popd
        mv "$pkgdir"/usr/share/{,cygwin-}terminfo
    fi
    rm -rf "$pkgdir"/usr/share/terminfo
    pushd "$pkgdir"/usr/${_target}
      mkdir -p share && cd share
      ln -s /usr/share/cygwin-terminfo/ terminfo
    popd

    # header compatibility
    pushd "$pkgdir"/usr/${_target}/include
      for _incdir in ncurses ncursesw; do
        mkdir "$_incdir" && pushd "$_incdir"
          ln -s ../*.h .
        popd
      done
    popd

    # fool packages looking to link to non-wide-character ncurses libraries
    pushd "$pkgdir"/usr/${_target}/lib
      for _f in *.a; do
        ln -s ${_f} ${_f/w/}
      done
      pushd pkgconfig
        for _f in *.pc; do
          ln -s ${_f} ${_f/w/}
        done
      popd
      # some packages look for -lcurses during build
      ln -s libncursesw.a libcurses.a
      ln -s libncursesw.dll.a libcurses.dll.a
    popd

    # fixup config
    pushd "$pkgdir"/usr/${_target}/bin
      mv ncursesw10-config ncursesw6-config
      sed -i -e 's/echo "10"/echo "6"/' ncursesw6-config
      sed -i -e "s|echo \"/usr/share/terminfo\"|echo \"/usr/${_target}/share/terminfo\"|g" ncursesw6-config
      ln -s ncursesw6-config ncurses6-config
    popd
  done

  install -vDm 644 COPYING -t "$pkgdir"/usr/share/licenses/${pkgname}/
}
