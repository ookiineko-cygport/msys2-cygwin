pkgname=cocom
pkgver=0.996
pkgrel=1
pkgdesc="Toolset for creation of compilers and interpreters"
arch=('x86_64')
url="https://cocom.sourceforge.io/"
license=('custom')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e143ab556d79a35ef31ec1e240897c9b8a8d0b6693e50a2b6e47d7fd4b200519')

prepare() {
  # top-level configure is 2.5x, but REGEX/configure is 2.13
  cd "$srcdir"/${pkgname}-${pkgver}/REGEX
  autoreconf -fi
}

build() {
  export CFLAGS+=" -Wno-error=format-security"
  export CXXFLAGS+=" -Wno-error=format-security"

  cd "$srcdir"/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
