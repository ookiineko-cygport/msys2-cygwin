pkgbase=cygwin-util-linux
pkgname=(cygwin-util-linux-libs)
pkgver=2.39.3
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (Cygwin)"
arch=('any')
url="https://github.com/util-linux/util-linux"
license=(
  'BSD-2-Clause'
  'BSD-3-Clause'
  'BSD-4-Clause-UC'
  'GPL-2.0-only'
  'GPL-2.0-or-later'
  'GPL-3.0-or-later'
  'ISC'
  'LGPL-2.1-or-later'
  'LicenseRef-PublicDomain'
)
groups=('cygwin-toolchain')
makedepends=('cygwin-configure')
options=('!strip' 'staticlibs' '!buildflags' '!emptydirs')
source=("https://www.kernel.org/pub/linux/utils/util-linux/v${pkgver%.*}/util-linux-${pkgver}.tar.xz"
        "util-linux-BSD-2-Clause.txt::https://raw.githubusercontent.com/Cyan4973/xxHash/f035303b8a86c1db9be70cbb638678ef6ef4cb2d/LICENSE"
        cygwin-include.patch
        cygwin-libblkid-topology-v2.patch)
sha256sums=('7b6605e48d1a49f43cc4b4cfc59f313d0dd5402fa40b96810bd572e167dfed0f'
            '6ffedbc0f7878612d2b23589f1ff2ab15633e1df7963a5d9fc750ec5500c7e7a'
            '224505c3624f61f085130b4bc440c6237b19536bcdda57599ea4100f8b704d42'
            'a445c73c38e23f7c7c3ca4efcdca46f82bd95db4b7f0b75cd463c7b81a6a95be')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/util-linux-${pkgver}

  patch -Np2 -i ../cygwin-include.patch
  patch -Np2 -i ../cygwin-libblkid-topology-v2.patch

  autoreconf -fi
}

build() {
  export CFLAGS+=" -D_GNU_SOURCE -Wno-char-subscripts -Wno-unused-parameter"
  export CPPFLAGS+=" -D__USE_LINUX_IOCTL_DEFS"

  cd "$srcdir"/util-linux-${pkgver}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure \
        --disable-all-programs \
        --enable-libuuid \
        --enable-libblkid \
        --enable-libsmartcols \
        --enable-libfdisk \
        SOLIB_LDFLAGS=-no-undefined
      make
    popd
  done
}

package_cygwin-util-linux-libs() {
  depends=('cygwin')
  conflicts=('cygwin-libutil-linux')
  provides=('cygwin-libutil-linux' 'cygwin-libblkid' 'cygwin-libfdisk' 'cygwin-libsmartcols' 'cygwin-libuuid')
  replaces=('cygwin-libutil-linux')

  pushd "$srcdir"/util-linux-${pkgver}
    for _target in ${_targets}; do
      echo "Installing for ${_target}"
      (cd build-${_target} && make DESTDIR="$pkgdir" install)
      # remove unnecessary files
      rm -rf "$pkgdir"/usr/${_target}/share
    done
  popd
  install -vDm 644 util-linux-${pkgver}/Documentation/licenses/COPYING.{BSD*,ISC} -t "$pkgdir"/usr/share/licenses/${pkgname}/
  install -vDm 644 util-linux-BSD-2-Clause.txt -t "$pkgdir"/usr/share/licenses/${pkgname}/
}
