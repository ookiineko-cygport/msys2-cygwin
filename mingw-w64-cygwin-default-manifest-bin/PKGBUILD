_realname=windows-default-manifest
pkgname=cygwin-default-manifest-bin
pkgver=6.4
pkgrel=1
pkgdesc="Default application manifest for the Cygwin cross toolchain (bootstrap)"
arch=('any')
url="https://cygwin.com/"
_mirror="https://mirrors.kernel.org/sourceware/cygwin"
license=('BSD')
provides=('cygwin-default-manifest')
conflicts=('cygwin-default-manifest')
options=('!strip' 'staticlibs' '!emptydirs')
_rev=2
source=("${_realname}-${pkgver}-${_rev}-x86_64.tar.xz::${_mirror}/x86_64/release/${_realname}/${_realname}-${pkgver}-${_rev}.tar.xz")
noextract=("${source[@]%%::*}")
sha256sums=('9a4da2154f812ab647dbbe7cc53f173b49fc9bf0e92f6f249186731ace1dc04c')

_targets="x86_64-pc-cygwin"

package() {
  for _target in ${_targets}; do
    echo "Extracting ${_target} tarball"
    mkdir -p "$pkgdir"/usr/${_target}
    tar -x -v -f "${_realname}-${pkgver}-${_rev}-${_target::-10}.tar.xz" -C "$pkgdir"/usr/${_target} --strip-components=1
    # install licenses
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/COPYING ]]; then
      install -Dm644 "$pkgdir"/usr/${_target}/share/doc/windows-default-manifest/COPYING \
          "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
    fi
    # remove unnecessary files
    rm -rf "$pkgdir"/usr/${_target}/share/doc/
  done
}
