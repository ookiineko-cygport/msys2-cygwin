pkgname=cygwin-protobuf
pkgver=21.12
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format (Cygwin)"
arch=('any')
url="https://developers.google.com/protocol-buffers/"
license=('BSD')
groups=('cygwin-toolchain')
depends=('cygwin' 'cygwin-zlib' 'gcc-libs' 'glibc' 'zlib')
makedepends=('cygwin-configure')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://github.com/protocolbuffers/protobuf/archive/v${pkgver}/protobuf-${pkgver}.tar.gz"
        "https://github.com/protocolbuffers/protobuf/commit/101b6199.patch"
        cygwin-remove-gtest.patch)
sha256sums=('22fdaf641b31655d4b2297f9981fa5203b2866f8332d3c6333f6b0107bb320de'
            '5eee1dd6898007307d9d286be8e45e5e5cf4d6e6d594fd5f49b236df4bfe2ac1'
            '9eff0831e3342f06773da2ed6758313910c677c57f7de5c0c0dd779796912d69')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/protobuf-${pkgver}

  patch -Rp1 -i ../101b6199.patch  # Revert ABI break
  patch -Np2 -i ../cygwin-remove-gtest.patch

  ./autogen.sh
}

build() {
  cd "$srcdir"/protobuf-${pkgver}
  mkdir -p build-host && pushd build-host
    echo "Building host protoc"
    ../configure --prefix=/usr --disable-shared --enable-static
    make -C src protoc
  popd
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure \
        --with-protoc="$srcdir"/protobuf-${pkgver}/build-host/src/protoc
      make
    popd
  done
}

package() {
  cd "$srcdir"/protobuf-${pkgver}
  echo "Installing host protoc"
  (cd build-host && install -vDm 755 src/protoc "$pkgdir"/usr/bin/cygwin-protoc)
  strip "$pkgdir"/usr/bin/cygwin-protoc
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    ${_target}-strip --strip-unneeded "$pkgdir"/usr/${_target}/bin/*.dll
    ${_target}-strip -g "$pkgdir"/usr/${_target}/lib/*.a
  done
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
