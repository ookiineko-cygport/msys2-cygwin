pkgbase=mingw-w64-cygwin
pkgname="${MINGW_PACKAGE_PREFIX}-cygwin"
pkgver=3.5.3
pkgrel=1
pkgdesc="POSIX compatibility C runtime for Windows"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://www.cygwin.com/"
msys2_references=('cpe: cpe:/a:cygwin:cygwin')
license=('GPL')
groups=("${MINGW_PACKAGE_PREFIX}-cygwin-toolchain")
makedepends=("${MINGW_PACKAGE_PREFIX}-perl"
             "${MINGW_PACKAGE_PREFIX}-cocom"
             "${MINGW_PACKAGE_PREFIX}-cygwin-gcc"
             "${MINGW_PACKAGE_PREFIX}-cygwin-binutils"
             "${MINGW_PACKAGE_PREFIX}-cygwin-w32api-runtime"
             "${MINGW_PACKAGE_PREFIX}-cygwin-w32api-headers"
             "${MINGW_PACKAGE_PREFIX}-autotools")
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
# XXX: MSYS2 sets these in /etc/makepkg_mingw.conf
unset CC CXX
# XXX: MSYS2 might set these in /etc/profile
unset PKG_CONFIG_PATH PKG_CONFIG_SYSTEM_INCLUDE_PATH PKG_CONFIG_SYSTEM_LIBRARY_PATH
_branch="${pkgname}-${pkgver}-0-gd8b21b8c7727"
source=("git+https://cygwin.com/git/newlib-cygwin#branch=${_branch}"
        fix-cygwin-sysroot-path-for-msys2-mingw-hosts.patch
        fix-perl-scripts-for-msys2-mingw-hosts.patch)
sha256sums=('SKIP'
            'de9c585a52fc453647878a64d35c4680ac4a9e146b9cf91314d4dd6a65eeeea3'
            'a44ad6893f619c2a4b3285aa4d8b47b7238989c95baafea32a8d0cea1001734a')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/newlib-cygwin

  patch -Np1 -i ../fix-cygwin-sysroot-path-for-msys2-mingw-hosts.patch
  patch -Np1 -i ../fix-perl-scripts-for-msys2-mingw-hosts.patch

  cd winsup
  ./autogen.sh
}

build() {
  # Windows 8.1 minimal supported systems
  export CFLAGS+=" -march=nocona -msahf"
  export CXXFLAGS+=" -march=nocona -msahf"

  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p "$srcdir"/build-${MSYSTEM}-${_target} && cd "$srcdir"/build-${MSYSTEM}-${_target}
    ../newlib-cygwin/configure --prefix=${MINGW_PREFIX}/${_target} \
        --host=${_target} --target=${_target} \
        --disable-dumper --disable-doc \
        --with-cross-bootstrap
    LC_ALL=C make
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    cd "$srcdir"/build-${MSYSTEM}-${_target}
    MSYS=winsymlinks:native \
    make DESTDIR="$pkgdir" tooldir=${MINGW_PREFIX}/${_target} install
    # install licenses
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/COPYING ]]; then
      install -Dm644 "${pkgdir}${MINGW_PREFIX}"/${_target}/share/doc/Cygwin/COPYING \
          "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
    fi
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/CYGWIN_LICENSE ]]; then
      install -Dm644 "${pkgdir}${MINGW_PREFIX}"/${_target}/share/doc/Cygwin/CYGWIN_LICENSE \
          "$pkgdir"/usr/share/licenses/${pkgname}/CYGWIN_LICENSE
    fi
    # remove unnecessary files
    rm -rf "${pkgdir}${MINGW_PREFIX}"/${_target}/etc/
    rm -rf "${pkgdir}${MINGW_PREFIX}"/${_target}/share/man/
    rm -rf "${pkgdir}${MINGW_PREFIX}"/${_target}/share/doc/
    rm -rf "${pkgdir}${MINGW_PREFIX}"/${_target}/share/cygwin/
    find "${pkgdir}${MINGW_PREFIX}"/${_target}/bin/ -type f ! -name \*.dll -delete
    # remove unwanted files
    rm -r "${pkgdir}${MINGW_PREFIX}"/${_target}/include/{rpc,iconv.h,unctrl.h}
  done
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
  # fixup symlinks
  for _symlink in $(find "$pkgdir" -type l); do
    _dest="$(readlink "$_symlink")"
    unlink "$_symlink"
    ln -s "$_dest" "$_symlink"
  done
}
